// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlinVersion = "1.3.71"

        ktLintVersion = "0.36.0"
        ktLintWrapperVersion = "9.2.1"
        detektVersion = "1.6.0"
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.google.gms:google-services:4.3.3"
        classpath "com.google.firebase:firebase-crashlytics-gradle:2.0.0-beta02"
    }
}

plugins {
    id "org.jlleitschuh.gradle.ktlint" version "$ktLintWrapperVersion"
    id "io.gitlab.arturbosch.detekt" version "$detektVersion"
}

apply from: rootProject.file("ktlint.gradle")
apply from: rootProject.file("versions.gradle")

allprojects {
    apply from: rootProject.file("detekt.gradle")
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

wrapper {
    gradleVersion = "6.2.2"
    distributionType = Wrapper.DistributionType.ALL
}

subprojects { subProject ->
    task allDependencies(type: DependencyReportTask) {}

    project.plugins.withId("com.android.library") {
        // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
        project.android.libraryVariants.all {
            it.generateBuildConfigProvider.configure { it.enabled = false }
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
