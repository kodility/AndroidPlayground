apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"

def appName = "kinofilm"

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildtoolsVersion

    defaultConfig {
        applicationId "com.androidplayground.kinofilm"

        minSdkVersion minSdk
        targetSdkVersion targetSdk

        versionCode 1
        versionName "1.0"

        //viewBinding.enabled = true
        dataBinding.enabled = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        setProperty("archivesBaseName", "${appName}-${versionName}")
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
        }
        release {
            def keystorePropertiesFile = rootProject.file("keystore/keystore.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            storeFile rootProject.file(keystoreProperties['keyStoreFile'])
            storePassword keystoreProperties['keyStorePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyAliasPassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"

            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation project(":lib-base")

    // Dagger
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Firebase SDK
    implementation "com.google.firebase:firebase-analytics:17.2.3"
    implementation "com.google.firebase:firebase-crashlytics:17.0.0-beta01"

    testImplementation "junit:junit:4.13"
    // optional - Test helpers for LiveData
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation("org.threeten:threetenbp:${threetenbpVersion}") {
        exclude module: "com.jakewharton.threetenabp:threetenabp:${threetenabpVersion}"
    }

    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
}
